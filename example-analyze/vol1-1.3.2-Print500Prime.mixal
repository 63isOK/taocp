* 500prime.mixal: find 500 prime,and print
* 
* convention:
* rI1 = l-500. (l is the number of primes to find)
* rI2 = n. (the number waiting for check IsPrime)
* rI3 = k. (the index of prime array, used in IsPrime)
* rI4 = B.
* rI5 = m + 50x.
*
* label   ins   operand     comment
L         EQU   500         the number of primes to find
PRINTER   EQU   18          line printer,word block is 24word
PRIME     EQU   -1          address of primes array
BUF0      EQU   2000        print buffer 0
BUF1      EQU   BUF0+25     print buffer 1
          ORIG  3000        start of program
START     IOC   0(PRINTER)  skip to new page
          LD1   =1-L=       rI1 = how many primes need to find
          LD2   =3=         rI2 = next number to be checked for prime
2H        INC1  1           rI2 is prime, rI1++
          ST2   PRIME+L,1   p[-1+L+rI1] = rI2, p is start from 0
          J1Z   2F          if find 500 prime, go to P9(print)
4H        INC2  2           rI2+=2, find next number to be checked for prime
          ENT3  2           rI3 = 2
6H        ENTA  0           rA = 0
          ENTX  0,2         rX = rI2
          DIV   PRIME,3     rAX/p[1...k]. (p[0]=2,p[1]=3)
          JXZ   4B          if rI2%3==0, find next number to be checked
          CMPA  PRIME,3     is check done?
          INC3  1           k++
          JG    6B          check whit next k
          JMP   2B          rI2 is a prime, find next
2H        OUT   TITLE(PRINTER)
          ENT4  BUF1+10     rI4 = 2035
          ENT5  -50         rI5 = -50
2H        INC5  L+1         rI5+=501, rI5=451
4H        LDA   PRIME,5     rA=p[rI5-1] (the first number is p[450])
          CHAR
          STX   0,4(1:4)    rX="str " (str is 4 char)
          DEC4  1           rI4--
          DEC5  50          rI5-=50
          J5P   4B          if a row is not ready, find next number
          OUT   0,4(PRINTER)
          LD4   24,4        rI4 = context(24+ BUF1)
          J5N   2B          if  print next row
          HLT

          ORIG  PRIME+1     address:0
          CON   2           the first prime
          ORIG  BUF0-5
TITLE     ALF   "FIRST"
          ALF   " FIVE"
          ALF   " HUND"
          ALF   "RED P"
          ALF   "RIMES"

          ORIG  BUF0+24     (LD4,CON) update rI4
          CON   BUF1+10
          ORIG  BUF1+24
          CON   BUF0+10
          END   START
